<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Ticker.Loader">
<Description>
Загрузчик данных о торговле акциями.
Описание API Информационно-статистического сервера Московской Биржи:
http://www.moex.com/a2193 и http://fs.moex.com/files/6523</Description>
<Abstract>1</Abstract>
<TimeCreated>64495,70547.718468</TimeCreated>

<Method name="Populate">
<Description>
Загрузить информацию о тикерах начиная с From и заканчивая To. Purge - удалить все записи перед началом загрузки
Формат From, To - YYYY-MM-DD
Write $System.Status.GetErrorText(##class(Ticker.Loader).Populate())</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>From:%Date(DISPLAY=3)="2013-03-25",To:%Date(DISPLAY=3)={$ZDate($Horolog,3)},Purge:%Boolean=$$$YES</FormalSpec>
<Implementation><![CDATA[
	#Dim Status As %Status = $$$OK
	Set FromH = $ZDateH(From, 3)
	Set ToH = $ZDateH(To, 3)
	
	Do:Purge ..Purge()
	
	For DateH = FromH:1:ToH {
		Write $c(13), "Populating ", $ZDate(DateH, 3)
		Set Status = ..PopulateDay(DateH)
		Quit:$$$ISERR(Status)
	}
	
	Quit Status
]]></Implementation>
</Method>

<Method name="PopulateDay">
<Description>
Загрузить данные за день. Данные загружаются страницами по 100 записей. 
Write $System.Status.GetErrorText(##class(Ticker.Loader).PopulateDay($Horolog))</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>DateH:%Date</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim Status As %Status = $$$OK
		
	Set Reader = ##class(%XML.Reader).%New()
	Set Date = $ZDate(DateH, 3) // Преобразовать дату из внутреннего формата в YYYY-MM-DD
	Set Count = 0 // Число загруженных записей
	
	While Count '= $G(CountOld) {
		Set CountOld = Count
		Set Status = Reader.OpenURL(..GetURL(Date, Count))
		Quit:$$$ISERR(Status)
		
		Do Reader.Correlate("row", "Ticker.Data")
		While Reader.Next(.Object, .Status) {
			#Dim Object As Ticker.Data
			
			If Object.Ticker '="" {
				Set Status = Object.%Save()
				Quit:$$$ISERR(Status)
				Set Count = Count + 1
			}
		}
		Quit:(Count-CountOld)<100 // На текущей странице меньше 100 записей => эта страница - последняя
	}
	Quit Status
]]></Implementation>
</Method>

<Method name="GetURL">
<Description>
Получить URL с информацией об акциях за дату Date, пропустить первые Start записей</Description>
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>Date,Start:%Integer=0</FormalSpec>
<Implementation><![CDATA[$$$FormatText("http://iss.moex.com/iss/history/engines/stock/markets/shares/boards/tqbr/securities.xml?date=%1&start=%2", Date, Start)
]]></Implementation>
</Method>

<Method name="Purge">
<Description>
Удалить все данные</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	Do ##class(Ticker.Data).%KillExtent()
]]></Implementation>
</Method>
</Class>
</Export>
