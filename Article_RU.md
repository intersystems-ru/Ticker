# Визуализация данных Московской Биржи с помощью InterSystems DeepSee

## Содержание

- Введение
- Получение данных
- ETL
- Построение куба
- Построение сводной таблицы
- Построение дашборда
- Установка MDX2JSON и DSW
- Визуализация
- Выводы
- Ссылки


## Введение

В стеке технологий InterSystems есть технология для разработки аналитических решений [DeepSee](http://www.intersystems.com/ru/our-products/embedded-technologies/deepsee/). Это встраиваемая аналитическая технология и набор инструментов для создания систем поддержки принятия эффективных решений, в том числе, и с применением прогнозных моделей.  DeepSee работает со структурированными и неструктурированными данными. Она предназначена для создания OLAP-решений для баз данных Caché и любых реляционных СУБД. InterSystems DeepSee предоставляет разработчикам средства для внедрения в свои приложения аналитической OLAP-функциональности, которая способна работать на оперативных базах данных приложений без создания отдельной инфраструктуры для решения аналитических задач.

В статье рассматривается пример создания в OLAP-куба, работа со средствами аналитики и построение пользовательского интерфейса на примере анализа котировок акций торгуемых на Московской Бирже.

## Получение данных

Для визуализации данных о котировках акций необходимо их сначала загрузить. У Московской Биржи есть публичное задокументированное [API](http://www.moex.com/a2193), которое предоставляет информацию о торговле акциями в форматах HTML, XML, JSON, CSV.  
Вот, к примеру, [XML данные](http://iss.moex.com/iss/history/engines/stock/markets/shares/boards/tqbr/securities.xml?date=2013-05-27) за 27 мая 2013 года. Создадим [XML-Enabled](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=GXML_import) класс `Ticker.Data` в платформе InterSystems:

```
Class Ticker.Data Extends (%Persistent, %XML.Adaptor)
{

/// Дата торгов
Property Date As %Date(FORMAT = 3, XMLNAME = "TRADEDATE", XMLPROJECTION = "attribute");

/// Краткое название компании
Property Name As %String(XMLNAME = "SHORTNAME", XMLPROJECTION = "attribute");

/// Тикер
Property Ticker As %String(XMLNAME = "SECID", XMLPROJECTION = "attribute");

/// Количество сделок
Property Trades As %Integer(XMLNAME = "NUMTRADES", XMLPROJECTION = "attribute");

/// Общая сумма сделок
Property Value As %Decimal(XMLNAME = "VALUE", XMLPROJECTION = "attribute");

/// Цена открытия
Property Open As %Decimal(XMLNAME = "OPEN", XMLPROJECTION = "attribute");

/// Цена закрытия
Property Close As %Decimal(XMLNAME = "CLOSE", XMLPROJECTION = "attribute");

/// Цена закрытия официальная
Property CloseLegal As %Decimal(XMLNAME = "LEGALCLOSEPRICE", XMLPROJECTION = "attribute");

/// Минимальная цена акции
Property Low As %Decimal(XMLNAME = "LOW", XMLPROJECTION = "attribute");

/// Максимальная цена акции
Property High As %Decimal(XMLNAME = "HIGH", XMLPROJECTION = "attribute");

/// Средневзвешенная цена акции http://www.moex.com/s1194
/// Может считаться как за день так и не за период.
Property Average As %Decimal(XMLNAME = "WAPRICE", XMLPROJECTION = "attribute");

/// Количество акций участвовавших в сделках
Property Volume As %Integer(XMLNAME = "VOLUME", XMLPROJECTION = "attribute");

}
```

И напишем загрузчик данных в формате XML. Так как класс у нас XML-Enabled то конвертация из XML в объекты класса Ticker.Data происходит автоматически. Аналогичного поведения можно достичь для данных в форматах JSON (через [динамические объекты](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=GJSON_create)) и CSV (используя [%SQL.Util.Procedures](http://docs.intersystems.com/latest/csp/documatic/%25CSP.Documatic.cls?PAGE=CLASS&LIBRARY=%25SYS&CLASSNAME=%25SQL.Util.Procedures#METHOD_CSVTOCLASS)). Так как API отдаёт данные за определённую дату (день) то нам надо итерировать по дням и сохранять поступающие данные. Кроме того данные о котировках акций приходят страницами по 100 записей. Загрузчик может выглядеть так:
```
/// Загрузить информацию об акциях начиная с From и заканчивая To. Purge - удалить все записи перед началом загрузки
/// Формат From, To - YYYY-MM-DD
/// Write $System.Status.GetErrorText(##class(Ticker.Loader).Populate())
ClassMethod Populate(From As %Date(DISPLAY=3) = "2013-03-25", To As %Date(DISPLAY=3) = {$ZDate($Horolog,3)}, Purge As %Boolean = {$$$YES})
{
	#Dim Status As %Status = $$$OK
	// Переводим даты во внутренний формат для простоты итерации
	Set FromH = $ZDateH(From, 3)
	Set ToH = $ZDateH(To, 3)
	
	Do:Purge ..Purge()
	
	For DateH = FromH:1:ToH {
		Write $c(13), "Populating ", $ZDate(DateH, 3)
		Set Status = ..PopulateDay(DateH)
		Quit:$$$ISERR(Status)
	}
	
	Quit Status
}

/// Загрузить данные за день. Данные загружаются страницами по 100 записей. 
/// Write $System.Status.GetErrorText(##class(Ticker.Loader).PopulateDay($Horolog))
ClassMethod PopulateDay(DateH As %Date) As %Status
{
	#Dim Status As %Status = $$$OK
		
	Set Reader = ##class(%XML.Reader).%New()
	Set Date = $ZDate(DateH, 3) // Преобразовать дату из внутреннего формата в YYYY-MM-DD
	Set Count = 0 // Число загруженных записей
	
	While Count '= $G(CountOld) {
		Set CountOld = Count
		Set Status = Reader.OpenURL(..GetURL(Date, Count)) // Получаем следующую страницу данных
		Quit:$$$ISERR(Status)
		
		// Устанавливаем соответствие нода row == объект класса Ticker.Data 
		Do Reader.Correlate("row", "Ticker.Data")
		
		// Десериализуем каждую ноду row в объект класса Ticker.Data
		While Reader.Next(.Object, .Status) {
			#Dim Object As Ticker.Data
			
			// Сохраняем объект
			If Object.Ticker '="" {
				Set Status = Object.%Save()
				Quit:$$$ISERR(Status)
				Set Count = Count + 1
			}
		}
		Quit:(Count-CountOld)<100 // На текущей странице меньше 100 записей => эта страница - последняя
	}
	Quit Status
}

/// Получить URL с информацией о котировках акций за дату Date, пропустить первые Start записей
ClassMethod GetURL(Date, Start As %Integer = 0) [ CodeMode = expression ]
{
$$$FormatText("http://iss.moex.com/iss/history/engines/stock/markets/shares/boards/tqbr/securities.xml?date=%1&start=%2", Date, Start)
}

```

Теперь загрузим данные командой: `Write $System.Status.GetErrorText(##class(Ticker.Loader).Populate())`

Весь код доступен в [репозитории](https://github.com/intersystems-ru/Ticker/).

## ETL

Как известно, для построения OLAP-куба в первую очередь необходимо сформировать [таблицу фактов](https://ru.wikipedia.org/wiki/Таблица_фактов): таблицу операций, записи которой требуется группировать и фильтровать. Таблица фактов может быть связана с другими таблицами по схеме [звезда](https://ru.wikipedia.org/wiki/Схема_звезды) или [снежинка](https://ru.wikipedia.org/wiki/Схема_снежинки). 

Таблица фактов для куба обычно является результатом работы аналитиков и разработчиков по процессу, который называется [ETL](https://ru.wikipedia.org/wiki/ETL) (extract, transform, load). Т.е. из данных предметной области делается “выжимка” необходимых для анализа данных, и переносится в удобную для хранилища структуру "звезда"/"снежинка": факты и справочники фактов. 
В нашем случае этап ETL пропустим т.к. наш класс `Ticker.Data` уже находятся во вполне удобном для создания куба состоянии.

## Построение куба

[DeepSee Architect](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2DT_ch_cube) - это веб-приложение для создания OLAP-куба. Для перехода к DeepSee Architect откроем Портал Управления Системой → DeepSee → Выбор области →  Architect. Открывается рабочее окно Архитектора.

Возможно нужно будет выбрать область, которая поддерживает DeepSee. В том случае если вы не видите вашей области в списке областей DeepSee перейдите в Портал Управления Системой → Меню → Управление веб-приложениями → /csp/область, и там в поле Включен поставьте галочку DeepSee и нажмите кнопку сохранить. После этого выбранная область должна появиться в списке областей DeepSee. 

### Создаем новый куб. 

Нажав на кнопку "Создать" попадаем на экран создания нового куба, там необходимо установить следующие параметры:
- Имя куба - название куба используемое в запросах к нему
- Отображаемое Имя - локализуемое название куба (перевод осуществляется [стандартными механизмами](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2IMP_ch_localization) InterSystems)
- Источник Cube - использовать таблицу фактов или другой куб в качестве источника данных
- Исходный класс - если на предыдущем шаге был выбран класс, то указываем в качестве таблицы фактов класс Ticker.Data.
- Имя класса для куба - имя класса, в котором будет храниться определение куба. Создаётся автоматически
- Описание класса - произвольное описание

Вот как выглядит наш новый куб:

![Создание куба](https://habrastorage.org/web/2ca/a45/a95/2caa45a95954472d810ed477179e051c.PNG)


### Определяем свойства куба

После нажатия кнопки OK будет создан новый куб:

![Новый куб](https://habrastorage.org/web/15a/4f7/7de/15a4f77decd747edadeef95901389156.PNG)

Слева выводятся свойства базового и связанных с ним по “снежинке” классов, которые можно использовать при построении куба.
Центральная часть экрана - это скелет куба. Его можно наполнить свойствами класса с помощью drag-n-drop из области базового класса, либо добавляя элементы вручную. Основными элементами куба являются измерения, показатели и списки.

#### Измерения (Dimensions)
[Измерения](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2MODEL_ch_concepts) - это элементы куба, которые группируют записи таблицы фактов. В измерения обычно относят “качественные” атрибуты базового класса, которые разбивают все записи таблицы фактов по тем или иным срезам. Например нам бы хотелось группировать все факты по названиям инструментов и по датам.

Для разбиения фактов по тикерам прекрасно подойдет свойство Ticker.
Перетянем Ticker на область измерений  - в результате Архитектор добавит в куб измерение Ticker с одной иерархией H1 и одним уровнем Ticker. Укажем отображаемые названия в подписях к измерению и уровню.

![Измерение](https://habrastorage.org/web/58c/90a/1c3/58c90a1c35cd4a7ebe5d007290066f4f.PNG)

Измерения помимо группировки позволяют строить иерархии вложенности фактов от общего к частному. Типичным примером является измерение по дате, которое обычно часто требуется представить в виде иерархии Год-Месяц-День.
Для свойств типа дата(например как у свойства Date тип %Date) в DeepSee есть специальный тип измерения time, в котором уже предусмотрены часто используемые функции для создания иерархий по дате. Воспользуемся этим и построим трехуровневую иерархию Год-месяц-день с помощью свойства Date.

![Измерение 2](https://habrastorage.org/web/4da/b21/153/4dab21153a8241b9b8e56d3b8d8e1f77.PNG)

Заметим, что в измерении есть элементы: собственно измерение, иерархия и уровни этой иерархии (Level). Любое измерение куба состоит как минимум из одной иерархии в котором в простейшем случае всего один уровень. 

#### Показатели (Measures)
[Показатели](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2MODEL_ch_concepts#D2MODEL_concepts_measure) или метрики это такие элементы куба, куда относят какие-либо "количественные" данные, которые необходимо посчитать для "качественных" измерений куба (Dimensions). 
Например в таблице фактов такими показателями могут быть свойства Volume (количество акций) и Average (Средняя цена). Перетянем свойство Volume на область показателей и создадим показатель "Количество" с функцией SUM, которая будет считать общее количество акций в текущем срезе. 
Добавим также в показатели свойство Average и укажем в качестве функции расчета MAX - расчет максимального значения. С целью использования цены для визуализации изменения максимальной цены акции во времени. 

![Показатели](https://habrastorage.org/web/8a8/210/04c/8a821004c7134f41ad629379908a4ecb.PNG)

#### Списки (Listings)

[Списки](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2MODEL_ch_concepts#D2MODEL_concepts_listing) -  это элементы куба, описывающие способ доступа к исходным данным куба, позволяя перейти от агрегированных к исходным данным куба. Как правило при работе с кубом, аналитик просматривает агрегированную информацию в различных срезах. Однако, часто возникает необходимость посмотреть на исходные факты, которые вошли в текущий срез. Для этого и создаются листинги - они перечисляют набор полей таблицы фактов, который нужно отобразить при переходе к просмотру фактов [Drillthrough](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2ANLY_ch_adhoc). Создадим простой листинг нажав кнопку "Добавить элемент":
![Листинг 1](https://habrastorage.org/web/db8/806/6ae/db88066ae5c544f0856c7e131d030563.PNG)

Теперь зададим поля таблицы фактов, которые надо выводить. Например выведем информацию о тикерах и колебаних их цены за день (Name, Ticker, "Open", CloseLegal, Low, Average, High):

![Листинг 2](https://habrastorage.org/web/36b/930/ca5/36b930ca59fe47b2962feecb08bddeda.PNG)

#### Компиляция куба

Итак мы добавили в куб два показателя, два измерения и один листинг - этого вполне достаточно и уже можно посмотреть, что получилось. 
Скомпилируем класс куба (Кнопка "Компилировать"). Если ошибок компиляции нет, значит куб создан правильно и можно наполнить его данными. 
Для этого нужно нажать "Построить куб" - в результате DeepSee загрузит данные из таблицы фактов в хранилище данных куба. 
Для работы с данными куба нам пригодится другое веб-приложение - DeepSee Analyzer.

## Построение сводной таблицы (Pivot)

[DeepSee Analyzer](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2ANLY_ch_intro) - визуальное средство для непосредственного анализа данных кубов и подготовки источников данных для дальнейшей визуализации. Для перехода к DeepSee Analyzer откроем Портал Управления Системой → DeepSee → Выбор области →  Analyzer. Открывается рабочее окно Аналайзера.
![Analyzer](https://habrastorage.org/web/ba5/c87/035/ba5c8703519e47eca0bbe27f49f19c72.PNG)
В рабочем окне Аналайзера слева мы видим элементы созданного куба: показатели и измерения. Комбинируя их мы строим запросы к кубу на языке [MDX](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2GMDX_ch_intro#D2GMDX_intro_mdx) - аналоге языка SQL для многомерных OLAP кубов.
Рассмотрим интерфейс Аналайзера. Справа - поле сводной таблицы. В поле сводной таблицы Аналайзера всегда показывается результат выполнения MDX-запроса. Посмотреть текущий MDX-запрос можно если нажать кнопку ![Source](https://habrastorage.org/web/29d/377/fed/29d377fed2d94b79a83c5287b4932a32.PNG). При первом открытии куба в поле сводной таблицы по умолчанию показывается  количество записей в таблице фактов - в нашем случае это количество записей в классе Ticker.Data. Этому соответствует MDX: `SELECT FROM [TICKER]`.

Чтобы создать сводную таблицу перетянем в поле колонок измерение “Год”. Показателем выберем "Объём". В результате получим таблицу количества проданных акций по годам. 
![Pivot 1](https://habrastorage.org/web/a03/ea2/e71/a03ea2e711634b8ba86dbee9ec4d17c0.PNG)
Далее перетянем измерение “Тикер” в поле колонок и получим уже сводную таблицу количества акций по инструментам, с разбиением по годам:
![Pivot 2](https://habrastorage.org/web/8f1/e07/9e8/8f1e079e8ed6489384b180f8da742a3d.PNG)

Сейчас для каждой ячейки полученной таблицы рассчитывается одна величина  - суммарное количество акций участвовавших в сделках (в случае если не выбран ни один показатель, считается количество фактов - в данном случае это можно интерпретировать как количество дней торговли инструмента). Это можно изменить. Добавим показатель "Средняя цена". В результате можно видеть уже более интересную картину: сводная таблица отображает среднюю максимум цены по каждому инструменту за год.

![Pivot 3](https://habrastorage.org/web/acb/216/e95/acb216e95ab145628329ff7c271d66e7.PNG)

Как мы помним, в определении куба у нас заложена иерархия по датам. Это значит что по измерению Дата возможна операция DrillDown (переход по иерархии измерения от общего к частному). В Аналайзере двойной щелчок по заголовку измерения приводит переходу к следующему по иерархии измерению (DrillDown). В данном случае двойной клик по году приведет к переходу к месяцам этого года, а двойной клик на месяце - к переходу на уровень дней. В итоге можно посмотреть как менялась средняя цена акции для дней или месяцев.

![Pivot 4](https://habrastorage.org/web/fee/ee8/f20/feeee8f20b4b40beb1404492a14f454b.PNG)


На предыдущем этапе мы создали листинг - инструмент перехода от агрегированных данных к исходным фактам. Выберем любую строку сводной таблицы и нажмём кнопку ![Drillthrough](https://habrastorage.org/web/814/84b/ed0/81484bed028041ed828768093de4c131.PNG) для перехода к листингу:
![Drillthrough 2](https://habrastorage.org/web/d1a/a45/2b6/d1aa452b69844a43a7272129697631fb.PNG)

Следующий этап - визуализация. Перед сохранением упростим сводную таблицу и сохраним её под именем TickersByYears.
![Pivot 5](https://habrastorage.org/web/dd6/9d1/3e6/dd69d13e6fd74bf985942cb535d75f01.PNG)

## Построение дашборда (Dashboard)

Портал Пользователя - это веб-приложение для создания и использования дашбордов ([панелей индикаторов](https://ru.wikipedia.org/wiki/Панель_индикаторов)). Дашборды содержат виждеты: таблицы, графики и карты на основе сводных таблиц, созданных аналитиками в Аналайзере.
Для перехода к Порталу Пользователя DeepSee откроем Портал Управления Системой → DeepSee → Выбор области →  Портал Пользователя. 
![Portal](https://habrastorage.org/web/f88/f5a/bdd/f88f5abdd50f4b64b3f66f6008b183d5.PNG)
Создадим новый дашборд нажав на стрелку справа → добавить → Добавить индикаторную панель
![Portal2](https://habrastorage.org/web/41c/186/875/41c186875ea14dbf8e782fc5d7877e8c.PNG)

Создадим виджет нажав на стрелку справа → Виджеты → "+" → Линейная диаграмма с маркерами. В качестве источника данных выберем TickersByYears:
![Widget](https://habrastorage.org/web/672/756/789/6727567890724f3bb65f58eb3a753d24.PNG)

Однако читатель возразит - это же средняя температура по больнице. И будет прав. Добавим фильтрацию по инструменту. Для этого нажмём стрелку справа → Виджеты → Виджет 1 → Элементы управления → "+". Форма создания нового фильтра выглядит следующим образом:
![Filter](https://habrastorage.org/web/fe8/559/69c/fe855969ca4e4f93bb5899527c2f1547.PNG)

А вот так выглядит наш виджет с фильтром. Пользователь может изменить значение фильтра на любое другое.
![Widget 2](https://habrastorage.org/web/2bb/ebe/f7e/2bbebef7e1714df996be80599da42005.PNG)

После этого сохраним дашборд.

## Установка MDX2JSON и DeepSeeWeb

Для визуализации созданного дашборда можно использовать следующие OpenSource решения:
- [MDX2JSON](https://github.com/intersystems-ru/Cache-MDX2JSON) - REST API предоставляет информацию о кубах, пивотах, дашбордах и многих других элементах DeepSee, в частности - результатах исполнения MDX запросов, что позволяет встраивать пользовательский интерфейс аналитического решения на DeepSee в любое современное Web или мобильное приложение.
- [DeepSeeWeb](https://github.com/intersystems-ru/DeepSeeWeb) - AngularJS приложение, предоставляющее альтернативную реализацию портала пользователя DeepSee. Может быть легко кастомизирован. Использует MDX2JSON в качестве бэкэнда. Вот пример дашборда визуализированного в DeepSeeWeb:

![DeepSeeWeb](https://habrastorage.org/web/815/5ab/c73/8155abc739b64bb1a4a90978f2ad2a18.png)

### Установка MDX2JSON

Для установки MDX2JSON надо:

1. Загрузить [Installer.xml](https://raw.githubusercontent.com/intersystems-ru/Cache-MDX2JSON/master/MDX2JSON/Installer.cls.xml) и импортировать его в любую область с помощью Studio, Портала Управления Системой или `Do $System.OBJ.Load(file)`.
2. Выполнить в терминале (пользователем с ролью %ALL): `Do ##class(MDX2JSON.Installer).setup()`

Для проверки установки надо открыть в браузере страницу `http://server:port/MDX2JSON/Test?Debug`. Возможно потребуется ввести логин и пароль (в зависимости от настроек безопасности сервера). Должна открыться страница с информацией о сервере. В случае получения ошибки, можно почитать на [Readme](https://github.com/intersystems-ru/Cache-MDX2JSON) и [Wiki](https://github.com/intersystems-ru/Cache-MDX2JSON/wiki/Installation-Guide---RU).

### Установка DeepSeeWeb

Для установки DeepSeeWeb надо:

1. Загрузить [установщик](https://github.com/intersystems-ru/DeepSeeWeb/releases) и импортировать его в любую область с помощью Studio, Портала Управления Системой или `Do $System.OBJ.Load(file)`.
2. Выполнить в терминале (пользователем с ролью %ALL): `Do ##class(DSW.Installer).setup()`

Для проверки установки  надо открыть в браузере страницу `http://server:port/dsw/index.html`. Должна открыться станица авторизации. В области SAMPLES представлено множество уже готовых дашбордов и все они автоматически отображаются в DeepSeeWeb.

## Визуализация

Откроем `http://server:port/dsw/index.html` и авторизируемся, также нужно указать область с кубом. Откроется список дашбордов, в нашем случае есть только один созданный дашборд "Акции". Откроем его:

![DSW](https://habrastorage.org/web/025/ee0/f99/025ee0f9992d44a7aed6948d6b70079f.PNG)

Отображается наш созданный виджет. Для него поддерживается Drilldown и фильтр созданный в Портале Пользователя DeepSee:

![DSW 2](https://habrastorage.org/web/8de/c01/fb0/8dec01fb0d7c49639043643256ad8821.PNG)


## Выводы

InterSystems DeepSee является мощным инструментом создания OLAP-решений, предоставляя разработчикам средства для создания и внедрения в свои приложения аналитической OLAP-функциональности, которая способна работать на оперативных базах данных приложений без создания отдельной инфраструктуры для решения аналитических задач.

## Ссылки
- [DeepSee](http://www.intersystems.com/ru/our-products/embedded-technologies/deepsee/)
- [Документация](http://docs.intersystems.com/latest/csp/docbook/DocBook.UI.Page.cls?KEY=D2MODEL_ch_concepts)
- [Репозиторий](https://github.com/intersystems-ru/Ticker)
- [MDX2JSON](https://github.com/intersystems-ru/Cache-MDX2JSON)
- [DeepSeeWeb](https://github.com/intersystems-ru/DeepSeeWeb)
- [API Московской Биржи](http://www.moex.com/a2193)
